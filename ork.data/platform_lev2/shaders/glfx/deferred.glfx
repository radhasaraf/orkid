///////////////////////////////////////////////////////////////
// FxConfigs
///////////////////////////////////////////////////////////////
fxconfig fxcfg_default {
  glsl_version = "330";
import "misctools.i";
}
///////////////////////////////////////////////////////////////
// Interfaces
///////////////////////////////////////////////////////////////
uniform_block ub_vtx {
  uniform mat4 MVPC;
  uniform mat4 MVPL;
  uniform mat4 MVPR;
}
///////////////////////////////////////////////////////////////
vertex_interface iface_vdefault : ub_vtx {
  in vec4 position : POSITION;
  in vec2 uv0 : TEXCOORD0;
  //
  out vec2 frg_uv;
}
///////////////////////////////////////////////////////////////

uniform_block ub_frg {
  uniform mat4 IVPArray[2];
  uniform sampler2D MapAlbedoAo;
  uniform sampler2D MapNormalL;
  uniform sampler2D MapDepth;
  uniform vec3 LightColor;
  uniform vec4 LightPosR;
  uniform vec2 InvViewportSize; // inverse target size
  uniform float Time;
}

fragment_interface iface_fdefault : ub_frg {
  in vec2 frg_uv;
  //
  out vec4 out_clr;
}
///////////////////////////////////////////////////////////////
vertex_shader vs_std : iface_vdefault {
  gl_Position = MVPC * position;
  frg_uv      = uv0;
}
///////////////////////////////////////////////////////////////
vertex_interface iface_vstereo : iface_vdefault {
  layout(secondary_view_offset=1) out highp int gl_Layer;
}
vertex_shader vs_std_stereo
: iface_vstereo
: extension(GL_NV_stereo_view_rendering)
: extension(GL_NV_viewport_array2)
{
  frg_uv      = uv0;
  ///////////////////////////
  gl_Position = MVPL * position;
  gl_SecondaryPositionNV = MVPR * position;
  gl_Layer = 0;
  gl_ViewportMask[0] = 1;
  gl_SecondaryViewportMaskNV[0] = 2;
  ///////////////////////////
}
///////////////////////////////////////////////////////////////
fragment_shader ps_baselight
  : extension(GL_ARB_fragment_layer_viewport)
  : iface_fdefault
  : lib_pointlight {
  vec3 albedo = texture(MapAlbedoAo, frg_uv).xyz;
  vec4 n_mdl  = texture(MapNormalL, frg_uv);
  vec3 n = normalize(n_mdl.xyz);
  float depthtex = texture(MapDepth, frg_uv).r;
  vec2 scrxy = (gl_FragCoord.xy*InvViewportSize)*2.0-vec2(1,1);
  vec3 chk = vec3(scrxy.x,scrxy.y,depthtex*2-1.0);
  vec3 wpos = wposxyz(chk).xyz;

  ///////////////////////////
  // test lights (until we hook in the lightmgr lights)
  ///////////////////////////

  //float ddd1 = clamp(1.0 - wpos_d.w * 0.0007, 0, 1);

  ////////////////////
  // start out with "sunlight"
  ////////////////////
  vec3 l = vec3((0.5 + dot(n, vec3(0, 1, 0)) * 0.5)) * 0.25;

  ////////////////////
  // headlight
  ////////////////////

  vec3 l2 = pointlight( wpos.xyz,
                        n,
                        LightPosR.xyz,
                        LightPosR.w, // radius
                        LightColor);

  //float dd = mod(length(wpos-User1.xyz),100)*0.01;

  ///////////////////////////

  vec3 c = albedo * (l+l2);
  out_clr = vec4(c,1);
}
///////////////////////////////////////////////////////////////
vertex_shader vs_pointlight : iface_vdefault {
  gl_Position = MVPC * position;
  frg_uv      = vec2(0,0);
}
///////////////////////////////////////////////////////////////
vertex_shader vs_pointlight_stereo
  : iface_vstereo
  : extension(GL_NV_stereo_view_rendering)
  : extension(GL_NV_viewport_array2) {
  frg_uv      = vec2(0,0);
  ///////////////////////////
  gl_Position = MVPL * position;
  gl_SecondaryPositionNV = MVPR * position;
  gl_Layer = 0;
  gl_ViewportMask[0] = 1;
  gl_SecondaryViewportMaskNV[0] = 2;
  ///////////////////////////
}
///////////////////////////////////////////////////////////////
fragment_shader ps_pointlight
  : extension(GL_ARB_fragment_layer_viewport)
  : iface_fdefault
  : lib_pointlight
   {
  vec2 UV = gl_FragCoord.xy*InvViewportSize;
  vec4 n_mdl  = texture(MapNormalL, UV);
  bool gotspot = length(n_mdl.xyz)>0.0;
  vec3 c = vec3(0,0,0);
  //if( gotspot ){
    float depthtex = texture(MapDepth, UV).r;
    vec2 scrxy = UV*2.0-vec2(1,1);
    vec3 chk = vec3(scrxy.x,scrxy.y,depthtex*2-1.0);
    vec3 wpos = wposxyz(chk).xyz;
    vec3 albedo = texture(MapAlbedoAo, UV).xyz;
    vec3 n = normalize(n_mdl.xyz);
    vec3 l = pointlight(wpos.xyz, n, LightPosR.xyz, LightPosR.w, LightColor );
    c =albedo*l;
  //}
  out_clr = vec4(c, 1);
}
///////////////////////////////////////////////////////////////
// StateBlocks
///////////////////////////////////////////////////////////////
state_block sb_default { inherits default; }
///////////////////////////////////////////////////////////////
technique baselight {
  fxconfig = fxcfg_default;
  pass p0 {
    vertex_shader   = vs_std;
    fragment_shader = ps_baselight;
    state_block     = sb_default;
  }
}
///////////////////////////////////////////////////////////////
technique baselight_stereo {
  fxconfig = fxcfg_default;
  pass p0 {
    vertex_shader   = vs_std_stereo;
    fragment_shader = ps_baselight;
    state_block     = sb_default;
  }
}
///////////////////////////////////////////////////////////////
technique pointlight {
  fxconfig = fxcfg_default;
  pass p0 {
    vertex_shader   = vs_pointlight;
    fragment_shader = ps_pointlight;
    state_block     = sb_default;
  }
}
///////////////////////////////////////////////////////////////
technique pointlight_stereo {
  fxconfig = fxcfg_default;
  pass p0 {
    vertex_shader   = vs_pointlight_stereo;
    fragment_shader = ps_pointlight;
    state_block     = sb_default;
  }
}
